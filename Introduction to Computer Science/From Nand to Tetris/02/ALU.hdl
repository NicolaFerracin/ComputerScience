// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
   // Put you code here:
   // check if need to zero x or y
	Mux16(a=x, b=false, sel=zx, out=zeroX);
	Mux16(a=y, b=false, sel=zy, out=zeroY);
	
	// check if need to negate x or y
	// first create not version then give it to the mux
	Not16(in=zeroX, out=NotZeroX);
	Mux16(a=zeroX, b=NotZeroX, sel=nx, out=processedX);
	Not16(in=zeroY, out=NotZeroY);
	Mux16(a=zeroY, b=NotZeroY, sel=ny, out=processedY);
	
	// prepare x+y
	Add16(a=processedX, b=processedY, out=sum);
	// prepare x&y
	And16(a=processedX, b=processedY, out=and);
	// feed it to the function mux
	Mux16(a=and, b=sum, sel=f, out=funcXY);
	
	// prepare negation of funcXY and feed it to the Mux
	Not16(in=funcXY, out=NotFuncXY);
	Mux16(a=funcXY, b=NotFuncXY, sel=no, out[0..7]=out0, out[8..15]=out1, out[15]=out2, out=out);
	
	Or8Way(in=out0, out=Orout1);
	Or8Way(in=out1, out=Orout2);
	Or(a=Orout1, b=Orout2, out=isZero);
	Mux(a=true, b=false, sel=isZero, out=zr);
	
	Or(a=out2, b=out2, out=ng);
	
	
}